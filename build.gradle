plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id "io.mateo.cxf-codegen" version "2.3.0"
}

group = 'org.example'
version = '0.0.1-SNAPSHOT'

ext {
    cxfVersion = "4.0.4"
}

allprojects {
    java {
        sourceCompatibility = '21'
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    // A mirror of the Shibboleth maven repository for newer opensaml artifacts.
    maven {
        name = 'Shibboleth Maven Releases'
        credentials {
            username wpsRepositoryUser
            password wpsRepositoryPassword
        }
        url 'https://artifacts.westernacher.com/artifactory/build.shibboleth.net/'
    }
}

dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    cxfCodegen "jakarta.xml.ws:jakarta.xml.ws-api"
    cxfCodegen "jakarta.annotation:jakarta.annotation-api"
    cxfCodegen "codes.rafael.jaxb2_commons:jaxb2-fluent-api:3.0.0"
    cxfCodegen "codes.rafael.jaxb2_commons:jaxb2-basics:3.0.0"

    compileOnly 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation "org.apache.cxf:cxf-spring-boot-starter-jaxws:${cxfVersion}"
    implementation "org.apache.cxf:cxf-rt-frontend-jaxws:${cxfVersion}"
    implementation "org.apache.cxf:cxf-rt-transports-http:${cxfVersion}"
    implementation "org.apache.cxf:cxf-rt-features-logging:${cxfVersion}"
    implementation "org.apache.cxf:cxf-rt-ws-security:${cxfVersion}"
    implementation "org.apache.cxf:cxf-rt-security-saml:${cxfVersion}"
    implementation "org.apache.cxf.services.sts:cxf-services-sts-core:${cxfVersion}"
    implementation 'jakarta.xml.ws:jakarta.xml.ws-api'
    implementation "jakarta.xml.bind:jakarta.xml.bind-api"
    implementation "codes.rafael.jaxb2_commons:jaxb2-basics-runtime:3.0.0"

    // https://github.com/google/guava/issues/6657
    implementation "com.google.guava:guava:32.1.3-jre"
    
    runtimeOnly 'jakarta.jws:jakarta.jws-api:3.0.0'
    runtimeOnly "jakarta.annotation:jakarta.annotation-api"

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly "org.apache.cxf:cxf-rt-ws-mex:${cxfVersion}"
}

// WSDL Java Classes Generation
import io.mateo.cxf.codegen.wsdl2java.Wsdl2Java

tasks.register("wsdl2javaDoubleItService", Wsdl2Java) {
    toolOptions {
        wsdl.set("$projectDir/src/main/resources/DoubleIt.wsdl")
        wsdlLocation.set("classpath:DoubleIt.wsdl")
        extraArgs.add("-xjc-classpath")
        extraArgs.add("-xjc${project.configurations.cxfCodegen.asPath}")
        extraArgs.add("-xjc-Xfluent-api")
        extraArgs.add("-xjc-XtoString")
        extraArgs.add("-xjc-Xequals")
        extraArgs.add("-xjc-XhashCode")
    }
}


tasks.named('test') {
    useJUnitPlatform()
}
